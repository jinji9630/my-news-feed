import lib.ft4.auth;

@extend(auth.auth_handler)
function () = auth.add_auth_handler(
  flags = ["MySession"]
);

operation create_user(name, pubkey) {
    val account = create_account_with_auth(single_sig_auth_descriptor(pubkey, set(["A", "T", "MySession"])));
    create user ( name, pubkey, account );
}

operation make_post(content: text) {
  val account = auth.authenticate();
  require(content.size() <= 255, "A post cannot be longer than 255 letters");
  val user = user @ { account };
  create post ( user, content );
}

operation follow_user(follow_id: byte_array) {
  val account = auth.authenticate();
  val user = user @ { account };
  val follow = require_user(follow_id);
  create follower ( user = follow, follower = user );
}

operation unfollow_user(unfollow_id: byte_array) {
  val account = auth.authenticate();
  val user = user @ { account };
  val follow = require_user(unfollow_id);
  delete follower @? { .user == follow, .follower == user };
}